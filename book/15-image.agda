{-# OPTIONS --without-K --exact-split #-}

module book.15-image where

open import book.14-propositional-truncation-solutions public

--------------------------------------------------------------------------------

-- Section 14.4 The image of a map

--------------------------------------------------------------------------------

-- The universal property of the image

-- Definition 14.4.1

-- We introduce the image inclusion of a map.

comp-hom-slice :
  {l1 l2 l3 l4 : Level} {X : UU l1} {A : UU l2} {B : UU l3} {C : UU l4}
  (f : A â†’ X) (g : B â†’ X) (h : C â†’ X) â†’
  hom-slice g h â†’ hom-slice f g â†’ hom-slice f h
comp-hom-slice f g h j i =
  pair ( ( map-hom-slice g h j) âˆ˜
         ( map-hom-slice f g i))
       ( ( triangle-hom-slice f g i) âˆ™h
         ( (triangle-hom-slice g h j) Â·r (map-hom-slice f g i)))

id-hom-slice :
  {l1 l2 : Level} {X : UU l1} {A : UU l2} (f : A â†’ X) â†’ hom-slice f f
id-hom-slice f = pair id refl-htpy

is-equiv-hom-slice :
  {l1 l2 l3 : Level} {X : UU l1} {A : UU l2} {B : UU l3}
  (f : A â†’ X) (g : B â†’ X) â†’ hom-slice f g â†’ UU (l2 âŠ” l3)
is-equiv-hom-slice f g h = is-equiv (map-hom-slice f g h)

-- Definition 14.4.2

precomp-emb :
  { l1 l2 l3 l4 : Level} {X : UU l1} {A : UU l2} (f : A â†’ X)
  {B : UU l3} ( i : B â†ª X) (q : hom-slice f (map-emb i)) â†’
  {C : UU l4} ( j : C â†ª X) (r : hom-slice (map-emb i) (map-emb j)) â†’
  hom-slice f (map-emb j)
precomp-emb f i q j r =
  pair
    ( ( map-hom-slice (map-emb i) (map-emb j) r) âˆ˜
      ( map-hom-slice f (map-emb i) q))
    ( ( triangle-hom-slice f (map-emb i) q) âˆ™h
      ( ( triangle-hom-slice (map-emb i) (map-emb j) r) Â·r
        ( map-hom-slice f (map-emb i) q)))

universal-property-image :
  ( l : Level) {l1 l2 l3 : Level} {X : UU l1} {A : UU l2} (f : A â†’ X) â†’
  { B : UU l3} (i : B â†ª X) (q : hom-slice f (map-emb i)) â†’
  UU (lsuc l âŠ” l1 âŠ” l2 âŠ” l3)
universal-property-image l {X = X} f i q =
  ( C : UU l) (j : C â†ª X) â†’ is-equiv (precomp-emb f i q j)

-- Lemma 14.4.3

is-prop-hom-slice :
  { l1 l2 l3 : Level} {X : UU l1} {A : UU l2} (f : A â†’ X) â†’
  { B : UU l3} (i : B â†ª X) â†’ is-prop (hom-slice f (map-emb i))
is-prop-hom-slice {X = X} f i =
  is-prop-is-equiv
    ( (x : X) â†’ fib f x â†’ fib (map-emb i) x)
    ( fiberwise-hom-hom-slice f (map-emb i))
    ( is-equiv-fiberwise-hom-hom-slice f (map-emb i))
    ( is-prop-Î 
      ( Î» x â†’ is-prop-Î 
        ( Î» p â†’ is-prop-map-is-emb (is-emb-map-emb i) x)))

-- Proposition 14.4.4

-- Proposition 14.4.4 condition (ii)

universal-property-image' :
  ( l : Level) {l1 l2 l3 : Level} {X : UU l1} {A : UU l2} (f : A â†’ X) â†’
  { B : UU l3} (i : B â†ª X) (q : hom-slice f (map-emb i)) â†’
  UU (lsuc l âŠ” l1 âŠ” l2 âŠ” l3)
universal-property-image' l {X = X} f i q =
  ( C : UU l) (j : C â†ª X) â†’
    hom-slice f (map-emb j) â†’ hom-slice (map-emb i) (map-emb j)

{- We show that condition (ii) implies the universal property of the image
   inclusion. The other direction of the equivalence is trivial and never 
   needed. -}

universal-property-image-universal-property-image' :
  ( l : Level) {l1 l2 l3 : Level} {X : UU l1} {A : UU l2} (f : A â†’ X) â†’
  { B : UU l3} (i : B â†ª X) (q : hom-slice f (map-emb i)) â†’
  universal-property-image' l f i q â†’ universal-property-image l f i q
universal-property-image-universal-property-image' l f i q up' C j =
  is-equiv-is-prop
    ( is-prop-hom-slice (map-emb i) j)
    ( is-prop-hom-slice f j)
    ( up' C j)

-- Example 14.4.5

universal-property-image-has-section :
  (l : Level) {l1 l2 : Level} {X : UU l1} {A : UU l2} (f : A â†’ X) â†’
  sec f â†’ universal-property-image l f emb-id (pair f refl-htpy)
universal-property-image-has-section l f (pair g H) =
  universal-property-image-universal-property-image'
    l f emb-id (pair f refl-htpy)
    ( Î» B m h â†’ pair ((pr1 h) âˆ˜ g) ( Î» x â†’ (inv (H x)) âˆ™ (pr2 h (g x))))

universal-property-image-is-emb :
  (l : Level) {l1 l2 : Level} {X : UU l1} {A : UU l2} (f : A â†’ X) â†’
  (H : is-emb f) â†’ universal-property-image l f (pair f H) (pair id refl-htpy)
universal-property-image-is-emb l f H =
  universal-property-image-universal-property-image'
    l f (pair f H) (pair id refl-htpy)
    ( Î» B m h â†’ h)

-- Example 14.4.6

{- We show that a map A â†’ P into a proposition P is a propositional truncation
   if and only if P is the image of A in 1. -}

--------------------------------------------------------------------------------

-- The existence of the image

-- Definition 14.4.7

im :
  {l1 l2 : Level} {X : UU l1} {A : UU l2} (f : A â†’ X) â†’ UU (l1 âŠ” l2)
im {X = X} {A} f = Î£ X (Î» x â†’ type-trunc-Prop (fib f x))

inclusion-im :
  {l1 l2 : Level} {X : UU l1} {A : UU l2} (f : A â†’ X) â†’ im f â†’ X
inclusion-im f = pr1

map-im :
  {l1 l2 : Level} {X : UU l1} {A : UU l2} (f : A â†’ X) â†’ A â†’ im f
map-im f a = pair (f a) (unit-trunc-Prop (pair a refl))

triangle-im :
  {l1 l2 : Level} {X : UU l1} {A : UU l2} (f : A â†’ X) â†’
  f ~ (inclusion-im f âˆ˜ map-im f)
triangle-im f a = refl

hom-slice-im :
  {l1 l2 : Level} {X : UU l1} {A : UU l2} (f : A â†’ X) â†’
  hom-slice f (inclusion-im f)
hom-slice-im f = pair (map-im f) (triangle-im f)

-- Proposition 14.4.8

is-emb-inclusion-im :
  {l1 l2 : Level} {X : UU l1} {A : UU l2} (f : A â†’ X) â†’
  is-emb (inclusion-im f)
is-emb-inclusion-im f =
  is-emb-pr1-is-subtype (Î» x â†’ is-prop-type-trunc-Prop)

emb-im :
  {l1 l2 : Level} {X : UU l1} {A : UU l2} (f : A â†’ X) â†’ im f â†ª X
emb-im f = pair (inclusion-im f) (is-emb-inclusion-im f)

-- Theorem 14.4.9

fiberwise-map-universal-property-im :
  {l1 l2 l3 : Level} {X : UU l1} {A : UU l2} {B : UU l3} (f : A â†’ X) â†’
  (m : B â†ª X) (h : hom-slice f (map-emb m)) â†’
  (x : X) â†’ type-trunc-Prop (fib f x) â†’ fib (map-emb m) x
fiberwise-map-universal-property-im f m h x =
  map-universal-property-trunc-Prop
    { A = (fib f x)}
    ( fib-emb-Prop m x)
    ( Î» t â†’
      pair ( map-hom-slice f (map-emb m) h (pr1 t))
           ( ( inv (triangle-hom-slice f (map-emb m) h (pr1 t))) âˆ™
             ( pr2 t)))

map-universal-property-im :
  {l1 l2 l3 : Level} {X : UU l1} {A : UU l2} {B : UU l3} (f : A â†’ X) â†’
  (m : B â†ª X) (h : hom-slice f (map-emb m)) â†’ im f â†’ B
map-universal-property-im f m h (pair x t) =
  pr1 (fiberwise-map-universal-property-im f m h x t)

triangle-universal-property-im :
  {l1 l2 l3 : Level} {X : UU l1} {A : UU l2} {B : UU l3} (f : A â†’ X) â†’
  (m : B â†ª X) (h : hom-slice f (map-emb m)) â†’
  inclusion-im f ~ ((map-emb m) âˆ˜ (map-universal-property-im f m h))
triangle-universal-property-im f m h (pair x t) =
  inv (pr2 (fiberwise-map-universal-property-im f m h x t))

universal-property-im :
  (l : Level) {l1 l2 : Level} {X : UU l1} {A : UU l2} (f : A â†’ X) â†’
  universal-property-image l f (emb-im f) (hom-slice-im f)
universal-property-im l f =
  universal-property-image-universal-property-image'
    l f (emb-im f) (hom-slice-im f)
    ( Î» B m h â†’
      pair ( map-universal-property-im f m h)
           ( triangle-universal-property-im f m h))

{- We show some truncatedness results, so that we can use images as sets, and
   so on. -}

is-trunc-im :
  {l1 l2 : Level} (k : ğ•‹) {X : UU l1} {A : UU l2} (f : A â†’ X) â†’
  is-trunc (succ-ğ•‹ k) X â†’ is-trunc (succ-ğ•‹ k) (im f)
is-trunc-im k f = is-trunc-emb k (emb-im f) 

is-prop-im :
  {l1 l2 : Level} {X : UU l1} {A : UU l2} (f : A â†’ X) â†’
  is-prop X â†’ is-prop (im f)
is-prop-im = is-trunc-im neg-two-ğ•‹

is-set-im :
  {l1 l2 : Level} {X : UU l1} {A : UU l2} (f : A â†’ X) â†’
  is-set X â†’ is-set (im f)
is-set-im = is-trunc-im neg-one-ğ•‹

is-1-type-im :
  {l1 l2 : Level} {X : UU l1} {A : UU l2} (f : A â†’ X) â†’
  is-1-type X â†’ is-1-type (im f)
is-1-type-im = is-trunc-im zero-ğ•‹

im-Set' :
  {l1 l2 : Level} (A : UU l2) (X : UU-Set l1) (f : A â†’ type-Set X) â†’
  UU-Set (l1 âŠ” l2)
im-Set' A X f = pair (im f) (is-set-im f (is-set-type-Set X))

im-Set :
  {l1 l2 : Level} (A : UU-Set l2) (X : UU-Set l1) (f : type-hom-Set A X) â†’
  UU-Set (l1 âŠ” l2)
im-Set A = im-Set' (type-Set A)

im-1-Type' :
  {l1 l2 : Level} (A : UU l2) (X : UU-1-Type l1)
  (f : A â†’ type-1-Type X) â†’ UU-1-Type (l1 âŠ” l2)
im-1-Type' A X f = pair (im f) (is-1-type-im f (is-1-type-type-1-Type X))

im-1-Type :
  {l1 l2 : Level} (A : UU-1-Type l2) (X : UU-1-Type l1)
  (f : type-hom-1-Type A X) â†’ UU-1-Type (l1 âŠ” l2)
im-1-Type A = im-1-Type' (type-1-Type A)

--------------------------------------------------------------------------------

-- The uniqueness of the image

-- Proposition 14.4.10

is-equiv-hom-slice-emb :
  {l1 l2 l3 : Level} {X : UU l1} {A : UU l2} {B : UU l3}
  (f : A â†ª X) (g : B â†ª X) (h : hom-slice (map-emb f) (map-emb g)) â†’
  hom-slice (map-emb g) (map-emb f) â†’
  is-equiv-hom-slice (map-emb f) (map-emb g) h
is-equiv-hom-slice-emb f g h i =
  is-equiv-has-inverse
    ( map-hom-slice (map-emb g) (map-emb f) i)
    ( Î» y â†’
      is-injective-emb g
      ( inv
        ( ( triangle-hom-slice
            ( map-emb g)
            ( map-emb f)
            ( i)
            ( y)) âˆ™
          ( triangle-hom-slice
            ( map-emb f)
            ( map-emb g)
            ( h)
            ( map-hom-slice (map-emb g) (map-emb f) i y)))))
    ( Î» x â†’
      is-injective-emb f
      ( inv
        ( ( triangle-hom-slice (map-emb f) (map-emb g) h x) âˆ™
          ( triangle-hom-slice (map-emb g) (map-emb f) i
            ( map-hom-slice
              ( map-emb f)
              ( map-emb g)
              ( h)
              ( x))))))

is-equiv-up-image-up-image :
  {l1 l2 l3 l4 : Level} {X : UU l1} {A : UU l2} (f : A â†’ X)
  {B : UU l3} (i : B â†ª X) (q : hom-slice f (map-emb i))
  {B' : UU l4} (i' : B' â†ª X) (q' : hom-slice f (map-emb i'))
  (h : hom-slice (map-emb i) (map-emb i'))
  (p : Id (comp-hom-slice f (map-emb i) (map-emb i') h q) q') â†’
  ({l : Level} â†’ universal-property-image l f i q) â†’
  ({l : Level} â†’ universal-property-image l f i' q') â†’
  is-equiv (map-hom-slice (map-emb i) (map-emb i') h)
is-equiv-up-image-up-image f i q i' q' h p up-i up-i' =
  is-equiv-hom-slice-emb i i' h (map-inv-is-equiv (up-i' _ i) q)

up-image-up-image-is-equiv :
  {l1 l2 l3 l4 : Level} {X : UU l1} {A : UU l2} (f : A â†’ X)
  {B : UU l3} (i : B â†ª X) (q : hom-slice f (map-emb i))
  {B' : UU l4} (i' : B' â†ª X) (q' : hom-slice f (map-emb i'))
  (h : hom-slice (map-emb i) (map-emb i'))
  (p : Id (comp-hom-slice f (map-emb i) (map-emb i') h q) q') â†’
  is-equiv (map-hom-slice (map-emb i) (map-emb i') h) â†’
  ({l : Level} â†’ universal-property-image l f i q) â†’
  ({l : Level} â†’ universal-property-image l f i' q')
up-image-up-image-is-equiv f i q i' q' h p is-equiv-h up-i {l} =
  universal-property-image-universal-property-image' l f i' q'
    ( Î» C j r â†’
      comp-hom-slice
        ( map-emb i')
        ( map-emb i)
        ( map-emb j)
        ( map-inv-is-equiv (up-i C j) r)
        ( pair
          ( map-inv-is-equiv is-equiv-h)
          ( triangle-section
            ( map-emb i)
            ( map-emb i')
            ( map-hom-slice (map-emb i) (map-emb i') h)
            ( triangle-hom-slice (map-emb i) (map-emb i') h)
            ( pair ( map-inv-is-equiv is-equiv-h)
                   ( issec-map-inv-is-equiv is-equiv-h)))))

up-image-is-equiv-up-image :
  {l1 l2 l3 l4 : Level} {X : UU l1} {A : UU l2} (f : A â†’ X)
  {B : UU l3} (i : B â†ª X) (q : hom-slice f (map-emb i))
  {B' : UU l4} (i' : B' â†ª X) (q' : hom-slice f (map-emb i'))
  (h : hom-slice (map-emb i) (map-emb i'))
  (p : Id (comp-hom-slice f (map-emb i) (map-emb i') h q) q') â†’
  ({l : Level} â†’ universal-property-image l f i' q') â†’
  is-equiv (map-hom-slice (map-emb i) (map-emb i') h) â†’
  ({l : Level} â†’ universal-property-image l f i q)
up-image-is-equiv-up-image f i q i' q' h p up-i' is-equiv-h {l} =
  universal-property-image-universal-property-image' l f i q
    ( Î» C j r â†’
      comp-hom-slice
        ( map-emb i)
        ( map-emb i')
        ( map-emb j)
        ( map-inv-is-equiv (up-i' C j) r)
        ( h))

--------------------------------------------------------------------------------

-- Section 14.5 Surjective maps

-- Definition 14.5.1

is-surjective :
  {l1 l2 : Level} {A : UU l1} {B : UU l2} â†’ (A â†’ B) â†’ UU (l1 âŠ” l2)
is-surjective {B = B} f = (y : B) â†’ type-trunc-Prop (fib f y)

-- Example 14.5.2

-- Proposition 14.5.3

dependent-universal-property-surj :
  (l : Level) {l1 l2 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B) â†’
  UU ((lsuc l) âŠ” l1 âŠ” l2)
dependent-universal-property-surj l {B = B} f =
  (P : B â†’ UU-Prop l) â†’
    is-equiv (Î» (h : (b : B) â†’ type-Prop (P b)) x â†’ h (f x))

is-surjective-dependent-universal-property-surj :
  {l1 l2 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B) â†’
  ({l : Level} â†’ dependent-universal-property-surj l f) â†’
  is-surjective f
is-surjective-dependent-universal-property-surj f dup-surj-f =
  map-inv-is-equiv
    ( dup-surj-f (Î» b â†’ trunc-Prop (fib f b)))
    ( Î» x â†’ unit-trunc-Prop (pair x refl))

square-dependent-universal-property-surj :
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B) â†’
  (P : B â†’ UU-Prop l3) â†’
  ( Î» (h : (y : B) â†’ type-Prop (P y)) x â†’ h (f x)) ~
  ( ( Î» h x â†’ h (f x) (pair x refl)) âˆ˜
    ( ( Î» h y â†’ (h y) âˆ˜ unit-trunc-Prop) âˆ˜
      ( Î» h y â†’ const (type-trunc-Prop (fib f y)) (type-Prop (P y)) (h y))))
square-dependent-universal-property-surj f P = refl-htpy

dependent-universal-property-surj-is-surjective :
  {l1 l2 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B) â†’
  is-surjective f â†’
  ({l : Level} â†’ dependent-universal-property-surj l f)
dependent-universal-property-surj-is-surjective f is-surj-f P =
  is-equiv-comp'
    ( Î» h x â†’ h (f x) (pair x refl))
    ( ( Î» h y â†’ (h y) âˆ˜ unit-trunc-Prop) âˆ˜
      ( Î» h y â†’ const (type-trunc-Prop (fib f y)) (type-Prop (P y)) (h y)))
    ( is-equiv-comp'
      ( Î» h y â†’ (h y) âˆ˜ unit-trunc-Prop)
      ( Î» h y â†’ const (type-trunc-Prop (fib f y)) (type-Prop (P y)) (h y))
      ( is-equiv-postcomp-Î 
        ( Î» y p z â†’ p)
        ( Î» y â†’
          is-equiv-diagonal-is-contr
            ( is-surj-f y)
            ( is-proof-irrelevant-is-prop
              ( is-prop-type-trunc-Prop)
              ( is-surj-f y))
            ( type-Prop (P y))))
      ( is-equiv-postcomp-Î 
        ( Î» b g â†’ g âˆ˜ unit-trunc-Prop)
        ( Î» b â†’ is-propositional-truncation-trunc-Prop (fib f b) (P b))))
    ( is-equiv-map-reduce-Î -fib f ( Î» y z â†’ type-Prop (P y)))

-- Corollary 14.5.4

is-surjective-is-propositional-truncation :
  {l1 l2 : Level} {A : UU l1} {P : UU-Prop l2} (f : A â†’ type-Prop P) â†’
    ({l : Level} â†’ dependent-universal-property-propositional-truncation l P f) â†’ is-surjective f
is-surjective-is-propositional-truncation f duppt-f =
  is-surjective-dependent-universal-property-surj f duppt-f

is-propsitional-truncation-is-surjective :
  {l1 l2 : Level} {A : UU l1} {P : UU-Prop l2} (f : A â†’ type-Prop P) â†’
    is-surjective f â†’ {l : Level} â†’ dependent-universal-property-propositional-truncation l P f
is-propsitional-truncation-is-surjective f is-surj-f =
  dependent-universal-property-surj-is-surjective f is-surj-f

-- Theorem 14.5.5

{-
is-surjective-universal-property-image :
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} {X : UU l3}
  (f : A â†’ X) (i : B â†ª X) (q : hom-slice f (map-emb i)) â†’
  ({l : Level} â†’ universal-property-image l f i q) â†’
  is-surjective (map-hom-slice f (map-emb i) q)
is-surjective-universal-property-image f i q up-i b = {!!}
-}

--------------------------------------------------------------------------------

-- Section 14.6 Cantor's diagonal argument

-- Definition 14.6.1

-- Theorem 14.6.2

map-cantor :
  {l1 l2 : Level} (X : UU l1) (f : X â†’ (X â†’ UU-Prop l2)) â†’ (X â†’ UU-Prop l2)
map-cantor X f x = neg-Prop (f x x)

iff-eq :
  {l1 : Level} {P Q : UU-Prop l1} â†’ Id P Q â†’ P â‡” Q
iff-eq refl = pair id id

no-fixed-points-neg-Prop :
  {l1 : Level} (P : UU-Prop l1) â†’ Â¬ (P â‡” neg-Prop P)
no-fixed-points-neg-Prop P = no-fixed-points-neg (type-Prop P)

not-in-image-map-cantor :
  {l1 l2 : Level} (X : UU l1) (f : X â†’ (X â†’ UU-Prop l2)) â†’
  ( t : fib f (map-cantor X f)) â†’ empty
not-in-image-map-cantor X f (pair x Î±) =
  no-fixed-points-neg-Prop (f x x) (iff-eq (htpy-eq Î± x))

cantor : {l1 l2 : Level} (X : UU l1) (f : X â†’ (X â†’ UU-Prop l2)) â†’
  Â¬ (is-surjective f)
cantor X f H =
  ( apply-universal-property-trunc-Prop
    ( H (map-cantor X f))
    ( empty-Prop)
    ( not-in-image-map-cantor X f))
